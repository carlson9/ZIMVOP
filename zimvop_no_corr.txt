model{
  for(i in 1:N){
  	S[i] <- max(min(S.star[i],1),0) # avoid unstable decimals
	  probit(S.star[i]) <- s.star[i]  # probability of being in visitability regime
    s.star[i] <- inprod(Z[i,], gamma[1:K])
    for(d in 1:D){
        y.star[i,d] <- inprod(X[i,,d], beta[1:J,d])
    }
    for(d in 1:D){
        for (i.cut in 1:n.cut){
          probit(Q[i,i.cut,d]) <- tau.unsorted[i.cut, d] - y.star[i,d]
        }
    }    
    for(d in 1:D){
        p[i,d,1] <- (1-S[i]) + S[i]*max(min(Q[i,1,d],1),0)
        p[i,d,2] <- S[i]*(max(min(Q[i,2,d],1),0) - max(min(Q[i,1,d],1),0))
        p[i,d,3] <- S[i]*(1 - max(min(Q[i,2,d],1),0))
        y[i,d] ~ dcat(p[i,d,])

    }

  }
  for(j in 1:J){
    for(d in 1:D){
      beta[j,d] ~ dnorm(0,0.0001)
    }
  }
  for(k in 1:K){
    gamma[k]  ~ dnorm(0,0.0001)
  }
  for(d in 1:D){
    tau.unsorted[1,d] <- 0
    for(i.cut in 2:n.cut){
      tau.unsorted[i.cut,d] <- tau[i.cut, d]
      tau[i.cut, d] ~ dlnorm(0,0.025)
    }

  }

}